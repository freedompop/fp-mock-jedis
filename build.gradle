plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'maven-publish'
}

project.ext {
    snapshot = project.property('build.snapshot').equals('true')
    artifactoryLibPath = snapshot ? "libs-snapshot-local" : "libs-release-local"
}

group = 'com.freedompop'
version = '2.6.0.0' + (snapshot ? '-SNAPSHOT' : '')

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

dependencies {
    compile 'redis.clients:jedis:2.6.0'
    testCompile 'junit:junit:4.11'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from project.tasks.javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task printConfiguration() {
    println "version: ${version}"
    println "snapshot: ${snapshot}"
    println "artifactoryLibPath: ${artifactoryLibPath}"
}

publishing {

    publications {

        mavenJava(MavenPublication) {
            from components.java

            artifact javadocJar
            artifact sourcesJar

            pom {
                name = 'fp-mock-jedis'
                description = 'Mock Jedis is a library for mocking out Jedis clients'
                url = 'https://github.com/freedompop/fp-mock-jedis'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://raw.github.com/freedompop/fp-mock-jedis/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'idyedov'
                        name = 'Ivan Dyedov'
                    }
                    developer {
                        id = 'snishimura'
                        name = 'Scott Nishimura'
                    }
                }

                scm {
                    url = 'scm:git@github.com:freedompop/fp-mock-jedis.git'
                    connection = 'scm:git@github.com:freedompop/fp-mock-jedis.git'
                    developerConnection = 'scm:git@github.com:freedompop/fp-mock-jedis.git'
                }
            }
        }
    }

    repositories {

        maven {

            credentials {
                username = project.property('artifactory.user')
                password = project.property('artifactory.password')
            }

            url = project.property('artifactory.url') + artifactoryLibPath
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
